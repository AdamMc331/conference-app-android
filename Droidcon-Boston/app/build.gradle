apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.mentalmachines.droidcon_boston"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 17
        versionName "2.0.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    signingConfigs {
        if (rootProject.file('upload.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('upload.properties')))
            release {
                storeFile rootProject.file(props.keyStore)
                storePassword props.keyStorePassword
                keyAlias props.keyAlias
                keyPassword props.keyAliasPassword
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (rootProject.file('upload.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            ext.enableCrashlytics = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    final support = "27.1.0"
    final playServices = "11.8.0"
    final junit = "4.12"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // TESTING
    testImplementation "junit:junit:$junit"

    // Support
    implementation "com.android.support:appcompat-v7:$support"
    implementation "com.android.support:support-v4:$support"
    implementation "com.android.support:cardview-v7:$support"
    implementation "com.android.support:design:$support"
    implementation "com.android.support:customtabs:$support"

    // Firebase
    implementation "com.google.firebase:firebase-messaging:$playServices"
    implementation "com.google.firebase:firebase-database:$playServices"
    implementation "com.google.firebase:firebase-core:$playServices"

    // Crashlytics only for release builds
    releaseImplementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }

    // Misc
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'eu.davidea:flexible-adapter:5.0.1'
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0-b3'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'com.dinuscxj:recyclerrefreshlayout:2.0.5'

    testImplementation "com.squareup.moshi:moshi:$moshi_version"
    testImplementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    testImplementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'