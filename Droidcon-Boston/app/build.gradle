apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

// Load variables defined in local.properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def fabricKey = properties.getProperty('FABRIC_KEY')

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.mentalmachines.droidcon_boston"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 2
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            manifestPlaceholders = [
                    fabricKey: fabricKey
            ]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    fabricKey: fabricKey
            ]
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'BASE_API_URL', BASE_API_URL
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    final support = "27.0.2"
    final playServices = "11.8.0"
    final butterKnife = "8.8.1"
    final junit = "4.12"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // TESTING
    testImplementation "junit:junit:$junit"

    // Support
    implementation "com.android.support:appcompat-v7:$support"
    implementation "com.android.support:support-v4:$support"
    implementation "com.android.support:cardview-v7:$support"
    implementation "com.android.support:design:$support"
    implementation "com.android.support:customtabs:$support"

    // Firebase cloud messaging
    implementation "com.google.firebase:firebase-messaging:$playServices"

    // Misc
    implementation "com.jakewharton:butterknife:$butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"
    implementation 'com.readystatesoftware.sqliteasset:sqliteassethelper:+'
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc1'
    implementation 'com.github.bumptech.glide:glide:3.7.0'


    implementation('com.twitter.sdk.android:twitter:2.3.2@aar') {
        transitive = true
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}


buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'